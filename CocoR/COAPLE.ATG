COMPILER Coaple
CHARACTERS
	letter = 'a' .. 'z' + 'A' .. 'Z'.
	digit = "0123456789".
	period = ".".
	tab = '\t'.
	cr = '\r'.
	lf = '\n'.
	slash = '/'.
	urlChar = '@'.
	apostr = '\''.
	quote = '\"'.
	underscore = '_'.
	neg = "-".
	stringChar = ANY - '"'.
TOKENS
	ident = (letter|underscore) {letter|digit|underscore}.
	number = [neg] digit {digit} [period digit {digit}].
	url = urlChar {letter|slash}.
	charVal = apostr letter apostr.
	stringVal = quote stringChar {stringChar} quote.
COMMENTS FROM "//" TO cr lf
COMMENTS FROM "/*" TO "*/" NESTED
IGNORE
	cr + lf + tab
PRODUCTIONS
	
	Coaple = Prog.
	
	Prog = 
		{TypeDec} {CasteDec}.
		
	TypeDec =
		"type" StructureType {StructureType} "end".
		
	TypeExp =
		PrimitiveType
		| StructureType
		| ID.
		
	PrimitiveType = 
		"integer"
		| "real"
		| "bool"
		| "char" 
		| "string".
		
	StructureType =
		RecordType
		| ListType 
		| EnumeratedType.
		
	RecordType =
		"record" ident "of" { ident {"," ident} ":" TypeExp ";" }
		"end".
		
	ListType =
		"list" ident "of" TypeExp "end".
		
	EnumeratedType =
		"enumerate" ident "=" IDList "end".
		
	IDList =
		ident {"," ident}.
		
	CasteDec =
		"caste" ident ["("[ParameterList]")"] [Inheritances] ";"
			[EnvironmentDecs]
			"state" [StateDecs]
			[ActionDecs]
			"init" [Statement]
			"body" 
				[LocalDecs]
				Statement
			"end" ident.
		
	Inheritances =
		"inherits" ident {"," ident}.
		
	EnvironmentDecs = 
		"observes" {EnvDec ";"}.
		
	EnvDec = 
		ident
		| "all"	ident "in" ID
		| "var" IDList "in" ID [":=" AgentID]
		| "set" IDList "in" ID [":=" AgentSetEnum].
	
	AgentID	=
		ident "in" ID.
		
	ID =
		ident [url].
		
	AgentSetEnum =
		"{"
			ident {"," ident}
		"}".

	StateDecs = 
		StateDec ";" {StateDec ";"}.
	
	StateDec = 
		"var" IDList ":" TypeExp [":=" ident].
		
	LocalDecs =
		LocalDec ";" {LocalDec ";"}.
	
	LocalDec = 
		"var" IDList ":" TypeExp [":=" ident].
		
	ActionDecs =
		ActionDec ";" {ActionDec ";"}.
	
	ActionDec =
		"action" IDList "("[ParameterList]")"
		[Statement]
		[Impact].
		
	ParameterList =
		Parameter {"," Parameter}.
	
	Parameter =
		ident {"," ident} ":" TypeExp.
		
	Impact =
		"affect" ID { "," ID }.
		
	Statement =
		Designator (":=" Exp | "(" [IDList] ")") ";"
		| CasteEvent ";"
		| AgentEvent ";"
		| Block
		| LoopStatement
		| IfStatement
		| CaseStatement
		| WithStatement
		| ForAllStatement
		| WhenStatement.
		
	Block =
		"begin" { Statement } "end".
		
	CasteEvent =
		"join" ID "("[ident]")"
		| "quit" ID
		| "suspend" ID
		| "resume" ID.
		
	AgentEvent =
		"create" ident "of" ID "("[ident]")" [url]
		| "destroy" ident.

	LoopStatement =
		ForLoop
		| WhileLoop
		| RepeatLoop
		| Loop.
	
	Loop =
		"loop" Statement "end".
		
	WhileLoop =
		"while" Conditions "do" Statement "end".
		
	RepeatLoop =
		"repeat" Statement "until" Conditions "end".
		
	ForLoop =
		"for" ident ":=" Exp "to" Exp ["by" Exp] "do"
		Statement
		"end".
		
	ForAllStatement =
		"forall" ident "in" Exp "do" 
		Statement 
		"end".
		
	IfStatement =
		"if" Conditions "then" Statement
		{"elseif" Conditions "then" Statement}
		["else" Statement]
		"end".
		
	CaseStatement =
		"case" Exp "of"
		{Exp "->" Statement}
		["else" Statement]
		"end".
		
	WithStatement =
		"with" Exp "do" Statement "end".
		
	WhenStatement =
		"when" {Scenario "->" Statement} "end".
		
	Scenario =
		(ident ["in" ID] ":" ActionPattern
		| "exist" ident "in" ID ":" ActionPattern
		| "all" ident "in" ID ":" ActionPattern)
		[("and"|"or"|"xor") Scenario].
		
	ActionPattern =
		ident "("[PatternPara {"," PatternPara}]")".
		
	PatternPara =
		Exp.
		
	Exp = 
		Term {Addop Term}.
		
	Conditions =
		Condition {Boolop Condition}.
		
	Condition =
		Exp [Relop Exp].
	
	Addop =
		"+" | "-" | "++".
		
	Mulop =
		"*" | "/" | "%".
		
	Relop =
		"=" | "!=" | ">" | ">=" | "<" | "<=".
		
	Boolop =
		"and" | "or".
		
	Term =
		Factor {Mulop Factor}.
	
	Factor = 
		number
		| ["not"] (Designator | "true" | "false")
		| "(" Exp ")"
		| charVal
		| stringVal.
		
	Designator =
		ident { "." ident } ["[" Exp "]"].
		

END Coaple.












